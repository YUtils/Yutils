apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'com.github.dcendents.android-maven'
group = 'com.github.yutils'
apply plugin: 'org.jetbrains.dokka'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 30
    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 30
        versionCode 25
        versionName "1.3.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        //build release 版本时开启lint检测
        checkReleaseBuilds true
        //lint 遇到error时继续构建，true为不继续构建(默认)，false为继续构建
        abortOnError false
    }

    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }

    buildFeatures {
        dataBinding = true
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.2.0'
    testImplementation 'junit:junit:4.13.1'
    androidTestImplementation 'androidx.test:runner:1.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
    //gson  <!-- https://mvnrepository.com/artifact/com.google.code.gson/gson -->
    implementation 'androidx.recyclerview:recyclerview:1.1.0'

    implementation 'com.github.yutils:yhttp:1.0.6'

    // https://mvnrepository.com/artifact/com.google.code.gson/gson
    implementation group: 'com.google.code.gson', name: 'gson', version: '2.8.6'

    // FST要用到以下3个包
    // https://mvnrepository.com/artifact/de.ruedigermoeller/fst
//    implementation group: 'de.ruedigermoeller', name: 'fst', version: '2.57'
    // https://mvnrepository.com/artifact/com.fasterxml.jackson.core/jackson-core
//    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.9.6'
    // https://mvnrepository.com/artifact/org.objenesis/objenesis
//    implementation group: 'org.objenesis', name: 'objenesis', version: '2.6'

    //____________________________________________________________RxJava____________________________________________________________
    //rxjava2，https://mvnrepository.com/artifact/io.reactivex.rxjava2/rxjava
    implementation group: 'io.reactivex.rxjava2', name: 'rxjava', version: '2.2.20'
    //rxandroid，https://mvnrepository.com/artifact/io.reactivex.rxjava2/rxandroid
    implementation group: 'io.reactivex.rxjava2', name: 'rxandroid', version: '2.1.1'
    //自动解绑，https://mvnrepository.com/artifact/com.trello.rxlifecycle3/rxlifecycle-components
    implementation group: 'com.trello.rxlifecycle3', name: 'rxlifecycle-components', version: '3.1.0'
    // https://mvnrepository.com/artifact/com.blankj/rxbus
    implementation group: 'com.blankj', name: 'rxbus', version: '1.6'
    //____________________________________________________________RxJava____________________________________________________________
}

//打包jar;doc;sources，请在Terminal中运行gradlew makeJar
def jarName = "yutils1.3.0"
task makeJar(type: Copy) {
    //删除存在的
    delete 'build/libs/' + jarName + ".jar"
    //设置拷贝的文件
    from("build/intermediates/aar_main_jar/release")
    //打进jar包后的文件目录,将classes.jar放入build/libs/目录下
    into('build/libs/')
    //要打包的jar文件
    include('classes.jar')
    //重命名
    rename('classes.jar', jarName + ".jar")
}
makeJar.dependsOn(build)
//-encoding UTF-8  -charset UTF-8
tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}
task generateSourcesJar(type: Jar) {
    group = 'jar'
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
    archiveName = jarName + '-sources.jar'
}
task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    options.encoding = "UTF-8"
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}
task dokkaJavadoc(type: org.jetbrains.dokka.gradle.DokkaTask) {
    outputFormat = "javadoc"
    outputDirectory = javadoc.destinationDir
}
task generateJavadoc(type: Jar, dependsOn: dokkaJavadoc) {
    group = 'jar'
    classifier = 'javadoc'
    from javadoc.destinationDir
    archiveName = jarName + '-javadoc.jar'
}
artifacts {
    archives generateJavadoc
    archives generateSourcesJar
}